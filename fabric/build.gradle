plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    // Test DataGen
    runs {
        // 添加一个新的 gradle 任务，会运行数据生成 API：“gradlew runDatagenClient”
        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated/client")}"
            vmArg "-Dfabric-api.datagen.strict-validation"

            runDir "build/datagen"
        }
        datagenServer {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.strict-validation"

            runDir "build/datagen"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }

    // dependencies
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${architectury_version}"

    // Development QOL
    modLocalRuntime "maven.modrinth:lazydfu:${lazydfu_version}"
    modImplementation("com.terraformersmc:modmenu:${modmenu_version}")

    // Trinkets
    modImplementation "dev.emi:trinkets:${trinkets_version}"

    // Integrations
    // JEI
      // compile against the JEI API but do not include it at runtime
    modCompileOnlyApi "mezz.jei:jei-${rootProject.minecraft_version}-common-api:${jei_version}"
    modCompileOnlyApi "mezz.jei:jei-${rootProject.minecraft_version}-fabric-api:${jei_version}"
      // at runtime, use the full JEI jar for Fabric
    modRuntimeOnly "mezz.jei:jei-${rootProject.minecraft_version}-fabric:${jei_version}"

    // REI
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    // EMI
    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    modRuntimeOnly "dev.emi:emi-fabric:${emi_version}"

    // MidnightLib
    modApi(include("maven.modrinth:midnightlib:${midnightlib_version}-fabric"))
    // Cloth Config
    modApi "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    // Geckolib
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.19:${geckolib_version}"
    // BookShelf
    modApi "net.darkhax.bookshelf:Bookshelf-Fabric-${rootProject.minecraft_version}:${bookshelf_version}"

    // Registrate
    modApi(include("com.tterrag.registrate_fabric:Registrate:${registrate_fabric_version}"))

    // Kiwi
    modApi "curse.maven:kiwi-fabric-549404:${kiwi_fabric_fileId}"
    annotationProcessor "curse.maven:kiwi-fabric-549404:${kiwi_fabric_fileId}"

    // Cardinal Components Api
    modApi(include("dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cardinal_components_api_version}"))

    // Milk Lib
    modApi(include("io.github.tropheusj:milk-lib:${milk_lib_version}"))

    // Reach Entity Attributes
    modApi(include("com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes_version}"))

    // Fabric Shields Lib
    modApi(include("com.github.CrimsonDawn45:Fabric-Shield-Lib:v${fabric_shield_lib_version}")) {
        exclude module: "midnightlib"
    }

    // Conditional Mixin
    modApi(include("com.github.Fallen-Breath:conditional-mixin:${coditionalmixin_version}")) {
        exclude module: "fabric-loader"
    }
}

processResources {
    // set up properties for filling into metadata
    Map<String, String> properties = Map.of(
            "version", version
    )
    properties.forEach((k, v) -> inputs.property(k, v))

    filesMatching("fabric.mod.json") {
        expand properties
    }
}

// 将数据生成的文件添加到 jar 中。
sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name + "-" + rootProject.minecraft_version
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        maven {
            name = "LocalRepo"
            url = "file://${rootProject.buildDir}/repo"
        }
    }
}
